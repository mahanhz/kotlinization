buildscript {
	ext {
		springBootVersion = '1.5.4.RELEASE'
		propDepsVersion = '0.0.8'
	}
	repositories {
		mavenCentral()

		maven {
			url 'https://plugins.gradle.org/m2/'
		}
		maven {
			url 'http://repo.spring.io/plugins-release'
		}
	}
	dependencies {
		classpath ("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath ("io.spring.gradle:propdeps-plugin:${propDepsVersion}")
	}
}

plugins {
	id "io.spring.dependency-management" version "1.0.3.RELEASE"
	id "org.jetbrains.kotlin.jvm" version "1.1.2-5"
	id "org.jetbrains.kotlin.plugin.spring" version "1.1.2-5"
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'kotlin'
	apply plugin: "kotlin-spring"

	apply plugin: 'propdeps'
	apply plugin: 'propdeps-maven'
	apply plugin: 'propdeps-idea'
	apply plugin: 'propdeps-eclipse'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
	}

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
	}
}

def frameworkAgnostic = [':application:core', ':application:adapter']
configure(subprojects - frameworkAgnostic.collect { project(it) }) {
	apply plugin: 'io.spring.dependency-management'

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
		}
	}
}

configure([project(':application')]) {
	apply plugin: 'org.springframework.boot'
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}

ext {
	versions = [
		commonsLang: '3.5',
		guava: '21.0',
		jacksonKotlin: '2.9.0.pr4',
		jug: '3.1.4',
		springfoxSwagger2: '2.7.0'
	]
	test_versions = [
		kotlinTest: '2.0.3'
	]
}

ext.app_tests = [
		spring_test: [
			"org.springframework.boot:spring-boot-starter-test",
		],
		unit_test: [
			"io.kotlintest:kotlintest:${test_versions.kotlinTest}",
			"org.jetbrains.kotlin:kotlin-test",
			"org.jetbrains.kotlin:kotlin-test-junit"
		]
]

apply from: 'gradle/application.gradle'
